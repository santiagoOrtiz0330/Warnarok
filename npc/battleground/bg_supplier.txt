//===== eAthena Script =======================================
//= BattleGround Supplier
//===== By: ==================================================
//= Brian
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= eAthena SVN (+ source edits)
//===== Description: =========================================
//= Sells consumables that can ONLY be used on 'bg_consume' maps.
//===== Additional Comments: =================================
//= http://www.eathena.ws/board/index.php?showtopic=268355
//= 1. To use, create a char named "Battleground"
//= 2. edit the #define BG_CHARID in battleground.h
//= 3. edit the 'set .@BG_CHARID' in this file (line 43)
//============================================================

prontera,156,181,3	script	Telma	701,{

	mes "[Telma]";
	mes "Welcome, mighty warrior.";
	mes "Do you need supplies for your battles?";
	mes "I can exchange supplies for your badges...";
	next;

	// Definir los consumibles en un array
	setarray .Consumables_BG[0], 
	    40001, 40,   // Mastela Fruit
	    40003, 50,   // Light Blue Potion
	    40000, 10,   // Speed Potion
	    40002, 50,   // Condensed White Potion
		40022, 5,	 // Royal Jelly
	    40021, 5,    // Poison Bottle
	    40023, 5,    // Light Berserk Potion
	    40024, 5,    // Light Awakening Potion
	    40053, 50,   // Fire Bottle
	    40054, 50,   // Acid Bottle
	    40056, 10,   // Glistening Coat
	    40057, 15,   // Plant Bottle
	    40052, 20,   // Blue Gemstone
	    40051, 20,   // Red Gemstone
	    40050, 20,   // Yellow Gemstone
	    40055, 10,   // Cobweb
	    40058, 20,   // Crystal Fragment
	    40009, 1,    // Whole Roast STR
	    40004, 1,    // Steamed Alligator with Vegetable AGI
	    40007, 1,    // Sumptuous Feast VIT
	    40008, 1,    // Special Royal Jelly Herbal Tea INT
	    40005, 1,    // Soul Haunted Bread DEX
	    40006, 1,    // Assorted Shish Kebob LUK
	    40013, 3,    // Fireproof Potion
	    40014, 3,    // Coldproof Potion
	    40015, 3,    // Earthproof Potion
	    40016, 3,    // Thunderproof Potion
	    40017, 3,    // Fire Elemental Converter
	    40018, 3,    // Water Elemental Converter
	    40019, 3,    // Earth Elemental Converter
	    40020, 3,    // Wind Elemental Converter
	    40010, 5,    // Box of Gloom
	    40012, 5,    // Box of Drowsiness
	    40011, 5;    // Box of Resentment


	// Menú dinámico
	set @choice, select(
	    "40 Mastela Fruit",
	    "50 Light Blue Potion",
	    "10 Speed Potion",
	    "50 Condensed White Potion",
	    "5 Royal Jelly",
	    "5 Poison Bottle",
	    "5 Light Berserk Potion",
	    "5 Light Awakening Potion",
	    "50 Fire Bottle",
	    "50 Acid Bottle",
	    "10 Glistening Coat",
	    "15 Plant Bottle",
	    "20 Blue Gemstone",
	    "20 Red Gemstone",
	    "20 Yellow Gemstone",
	    "10 Cobweb",
	    "20 Crystal Fragment",
	    "1 Whole Roast STR +7",
	    "1 Steamed Alligator with Vegetable AGI +7",
	    "1 Sumptuous Feast VIT +7",
		"1 Special Royal Jelly Herbal Tea INT +7",
	    "1 Soul Haunted Bread DEX +7",
	    "1 Assorted Shish Kebob LUK +7",
	    "3 Fireproof Potion",
	    "3 Coldproof Potion",
	    "3 Earthproof Potion",
	    "3 Thunderproof Potion",
	    "3 Fire Elemental Converter",
	    "3 Water Elemental Converter",
	    "3 Earth Elemental Converter",
	    "3 Wind Elemental Converter",
	    "5 Box of Gloom",
	    "5 Box of Drowsiness",
	    "5 Box of Resentment"
	);

	// Obtener el ID y la cantidad usando el índice
	callsub S_BuyConsumableBG, .Consumables_BG[(@choice - 1) * 2], .Consumables_BG[(@choice - 1) * 2 + 1];
	end;

S_BuyConsumableBG:
	set .@BG_CHARID, 150001; // character named "Battleground"

S_BuyConsumable:
	set .@item_id, getarg(0);
	set .@amt, getarg(1);
	
	mes "[Telma]";
	mes "How many sets of ^0000FF"+ .@amt +" "+ getitemname(.@item_id) +"s^000000 do you want?";
	mes "1 = "+ .@amt   +" supplies = 1 badge";
	mes "2 = "+ .@amt*2 +" supplies = 2 badges";
	mes "3 = "+ .@amt*3 +" supplies = 3 badges";
	mes "etc..";
	next;
	input .@sets;
	mes "[Telma]";
	if (.@sets < 1) {
		mes "Enter a positive amount, greater than zero.";
		close;
	}
	mes "So you want ^0000FF"+ (.@amt*.@sets) +" "+ getitemname(.@item_id) +"s^000000...";
	if (.@BG_CHARID) mes "Remember these can only be used in Battlegrounds.";
	mes "It will cost you "+ .@sets +" badge(s)...";
	next;
	switch(select("Give her "+ .@sets +" Bravery Badge(s):Give her "+ .@sets +" Valor Badge(s):Give her "+ (3*.@sets) +" Heroism Badges")) {
		case 1: set .@badge,7828; set .@cost,1; break;
		case 2: set .@badge,7829; set .@cost,1; break;
		case 3: set .@badge,7773; set .@cost,3; break;
	}
	mes "[Telma]";
	if (countitem(.@badge) < (.@cost*.@sets)) {
		mes "You do not have enough ^FF0000"+getitemname(.@badge)+"s^000000 to buy "+(.@amt*.@sets)+" "+getitemname(.@item_id)+"s.";
	} else if (!checkweight(.@item_id, .@amt*.@sets)) {
		mes "You are overweight or have too many items in your inventory.";
	} else {
		delitem .@badge, .@cost*.@sets;
		if (.@BG_CHARID) { // Battleground's Consumables
			getitem2 .@item_id,(.@amt*.@sets),1,0,0, 254, 0, .@BG_CHARID & 0xFFFF, .@BG_CHARID >> 0x10;
		} else {
			getitem .@item_id,(.@amt*.@sets);
		}
		mes "Here are your: ^0000FF"+(.@amt*.@sets)+" "+getitemname(.@item_id)+"s^000000!";
		if (.@BG_CHARID) mes "Remember, they can only be used in Battlegrounds.";
	}
	close;
}